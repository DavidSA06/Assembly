;COUNTS IN DECIMAL USING AN EIGHT SEGMENT DISPLAY
;MADE BY DAVID SILVA APANGO
		LIST P=16F84A
		INCLUDE "P16F84A.INC"

;;;;;;;;DEFINITIONS

COUNTER		EQU 0CH				;TIMER0 CYCLES
FLAG		EQU 0DH				;COUNT DOWN
DISPLAY		EQU 0EH				;UP COUNT, NEVER GETS TO 0

RESET
		ORG		00H
		GOTO	START
		
		ORG		08H

;;;;;;;;PIC CONFIGURATION

START	BSF		STATUS,RP0		;SELECT BANK 1
		MOVLW	B'00000111'
		MOVWF	OPTION_REG		;MAXIMUM PRESCALER RATE
		MOVLW	B'00000000'
		MOVWF	TRISB			;ALL PORTB AS OUTPUT
		BCF		STATUS,RP0		;SELECT BANK 2
		MOVLW	B'00000000'
		MOVWF	PORTB			;PORTB STARTS IN ZERO

;;;;;;;;LOAD VARIABLES

LOAD	MOVLW	.10
		MOVWF	FLAG			;LOAD FLAG (10) FOR DECREMENT

		MOVLW	.0
		MOVWF	DISPLAY			;LOAD  DISPLAY (0) FOR INCREMENT

COUNT	CALL	DELAY			;DELAY ~1 SECOND
		MOVF	DISPLAY,0		;MOVE CURRENT NUMBER TO W
		CALL	TABLE			;SELECT DISPLAY NUMBER
		MOVWF	PORTB			;CHANGE DISPLAY VALUE
		INCF	DISPLAY,1
		DECFSZ	FLAG,1			;WAIT FLAG RAISING
		GOTO	COUNT			;NEXT NUMBER
		GOTO	LOAD			;REPEAT FROM ZERO

DELAY	MOVLW	.20
		MOVWF	COUNTER			;LOAD COUNTER (20)
TEMP	MOVLW	.61
		MOVWF	TMR0			;LOAD TMR0 (61) ~50MS
SIGNAL	BTFSS	INTCON,T0IF		;IS T0IF SET?
		GOTO	SIGNAL			;NO, WAIT
		BCF		INTCON,T0IF		;CLEAR T0IF FLAG
		DECFSZ	COUNTER,1		;DECREMENT COUNTER
		GOTO	TEMP			;LOAD TMR0
		RETURN

TABLE	ADDWF PCL,F				;MAKE A COMPUTED GOTO
		RETLW B'00000010'		;0
		RETLW B'10011110'		;1
		RETLW B'00100100'		;2
		RETLW B'00001100'		;3
		RETLW B'10011000'		;4
		RETLW B'01001000'		;5
		RETLW B'01000000'		;6
		RETLW B'00011110'		;7
		RETLW B'00000000'		;8
		RETLW B'00001000'		;9


		END
