		LIST P=16F877A
		INCLUDE "P16F877A.INC"
		__CONFIG _CP_OFF & _WDT_OFF & _PWRTE_ON & _XT_OSC & _LVP_OFF & _BODEN_OFF

ANS1					EQU					20H								;INICIALIZACION DE VARIABLES
CONTBIT					EQU					21H
BYTE					EQU					22H
REGISTRO1				EQU					23H
REGISTRO2				EQU					24H
REGISTRO3				EQU					25H
REGISTRO4				EQU					26H
REGISTRO5				EQU					27H

RESET

						ORG					00H
						GOTO				INICIO

						ORG					08H

;;;;;;;;;;;;;;;;;;;;;;;;CONFIGURACION DE ENTRADAS DEL PIC

INICIO					BSF					STATUS,RP0						;CAMBIO AL BANCO 1
						BCF					STATUS,RP1						;CAMBIO AL BANCO 1
						MOVLW				B'00000000'
						MOVWF				TRISB							;PINES DE B COMO COMO SALIDAS PARA LED

;;;;;;;;;;;;;;;;;;;;;;;;CONFIGURACIÓN DEL PUERTO SERIAL

						MOVLW				B'10000000'
						MOVWF				TRISC
						MOVLW				D'25'							;9600  BAUDIOS
						;MOVLW				D'12'							;19200 BAUDIOS
						MOVWF				SPBRG							;BAUDIOS Fosc/(K*(X+1)) X=SPBRG
						BCF					TXSTA,TX9						;TRANSMISION A 8 BITS
						BCF					TXSTA,SYNC						;USART MODO ASINCRONO 
						BSF					TXSTA,BRGH						;BAUDIOS RATE HIGH SPEED K=16
						BSF					TXSTA,TXEN						;TRANSMISION HABILITADA
						MOVLW				B'00000001'
						MOVWF				OPTION_REG						;PORTB PULL-UP HABILITADO PREESCALADOR 1:4
						MOVLW				B'10000110'
						MOVWF				ADCON1
						BCF					STATUS,RP0						;CAMBIO AL BANCO 0
						BCF					STATUS,RP1						;CAMBIO AL BANCO 0
						CLRF				INTCON							;INTERRUPCIONES NO HABILITADAS
						BSF					RCSTA,SPEN						;PUERTO SERIAL HABILITADO
						BCF					RCSTA,RX9						;RECEPCION A 8 BITS
						BSF					RCSTA,CREN						;RECIBIMIENTO CONTINUO HABILITADO
						CLRF				PORTB							;REINICIO DE BYTES IMPORTANTES
						CLRF				REGISTRO1
						CLRF				REGISTRO2
						CLRF				REGISTRO3
						CLRF				REGISTRO4
						CLRF				REGISTRO5

;;;;;;;;;;;;;;;;;;;;;;;;RECEPCION DE DATOS

RCN						BTFSS				PIR1,RCIF						;BANDERA DE RECEPCION ?
						GOTO				RCN
						BCF					PIR1,RCIF						;BAJAR LA BANDERA DE RECEPCION
						MOVFW				RCREG							;REGISTRO DE RECEPCION DE USART
						MOVWF				ANS1							;MOVER A LA CONSTANTE ANS1

;;;;;;;;;;;;;;;;;;;;;;;;CAMBIAR A RUNNING STATUS (TRANSMISIÓN DEL PIC)

						BCF					INTCON,T0IF						;BAJAR LA BANDERA T0IF
						BSF					STATUS,RP0						;CAMBIO AL BANCO 1
						BCF					STATUS,RP1						;CAMBIO AL BANCO 1
						MOVLW				B'00000000'
						MOVWF				TRISA							;PINES DE A COMO SALIDA PARA DHT22
						BCF					STATUS,RP0						;CAMBIO AL BANCO 0
						BCF					STATUS,RP1						;CAMBIO AL BANCO 0
						BCF					PORTA,0							;ENVIAR SENAL GND AL SENSOR
						MOVLW				.5								;1004us (1000 MINIMO)
						MOVWF				TMR0							;COMIENZA EL CONTEO
						BTFSS				INTCON,T0IF						;REVISAR EL DESBORDAMIENTO
						GOTO				$-1
						BCF					INTCON,T0IF						;BAJAR LA BANDERA T0IF
						BSF					STATUS,RP0						;CAMBIO AL BANCO 1
						BCF					STATUS,RP1						;CAMBIO AL BANCO 1
						MOVLW				B'00000001'
						MOVWF				TRISA							;PIN 0 DE A COMO ENTRADA PARA DHT22
						BCF					STATUS,RP0						;CAMBIO AL BANCO 0
						BCF					STATUS,RP1						;CAMBIO AL BANCO 0
REVISION				BTFSC				PORTA,0							;ESPERAR A LA RESPUESTA BAJA DEL SENSOR POR 20~40 us
						GOTO				REVISION
REVISION80A				BTFSS				PORTA,0							;ESPERAR A LA RESPUESTA ALTA DEL SENSOR POR 80 us
						GOTO				REVISION80A 
REVISION80B				BTFSC				PORTA,0							;ESPERAR EL INICIO DE LA TRANSMISION POR 80 us
						GOTO				REVISION80B

;;;;;;;;;;;;;;;;;;;;;;;;LECTURA DE REGISTROS

;;;;;;;;;;;;;;;;;;;;;;;;PRIMER REGISTRO (HUMEDAD ALTA)
						CALL				CAPTURA
						MOVFW				BYTE
						MOVWF				REGISTRO1
;;;;;;;;;;;;;;;;;;;;;;;;SEGUNDO REGISTRO (HUMEDAD BAJA)
						CALL				CAPTURA
						MOVFW				BYTE
						MOVWF				REGISTRO2
;;;;;;;;;;;;;;;;;;;;;;;;TERCER REGISTRO (TEMPERTURA ALTA)
						CALL				CAPTURA
						MOVFW				BYTE
						MOVWF				REGISTRO3
;;;;;;;;;;;;;;;;;;;;;;;;CUARTO REGISTRO (TEMPERATURA BAJA)
						CALL				CAPTURA
						MOVFW				BYTE
						MOVWF				REGISTRO4
;;;;;;;;;;;;;;;;;;;;;;;;QUINTO REGISTRO (PARIDAD)
						CALL				CAPTURA
						MOVFW				BYTE
						MOVWF				REGISTRO5
;;;;;;;;;;;;;;;;;;;;;;;;TERMINA REGISTRO DE DATOS

;;;;;;;;;;;;;;;;;;;;;;;;TRANSMISION DE DATOS

;;;;;;;;;;;;;;;;;;;;;;;;PRIMER REGISTRO (HUMEDAD ALTA)
						MOVFW				REGISTRO1
						CALL				ENVIO
;;;;;;;;;;;;;;;;;;;;;;;;PRIMER REGISTRO HUMEDAD BAJA)
						MOVFW				REGISTRO2
						CALL				ENVIO
;;;;;;;;;;;;;;;;;;;;;;;;PRIMER REGISTRO (TEMPERTURA ALTA)
						MOVFW				REGISTRO3
						CALL				ENVIO
;;;;;;;;;;;;;;;;;;;;;;;;PRIMER REGISTRO (TEMPERATURA BAJA)
						MOVFW				REGISTRO4
						CALL				ENVIO
;;;;;;;;;;;;;;;;;;;;;;;;PRIMER REGISTRO (PARIDAD)
						MOVFW				REGISTRO5
						CALL				ENVIO
;;;;;;;;;;;;;;;;;;;;;;;;TERMINA TRANSMISION DE DATOS

						GOTO				RCN

;;;;;;;;;;;;;;;;;;;;;;;;SUBRUTINAS

;;;;;;;;;;;;;;;;;;;;;;;;SUBRUTINA DE CAPTURA DE DATOS POR BYTE

CAPTURA					CLRF				BYTE
						MOVLW				.7
						MOVWF				CONTBIT							;CARGAR CONTADOR DE BITS
CINCUENTA				BTFSS				PORTA,0							;ESPERAR 50 us
						GOTO				$-1
						BCF					INTCON,T0IF
						MOVLW				.245
						MOVWF				TMR0							;CONTAR 40us (26~28us A 70us)
						BTFSC				PORTA,0
						GOTO				$-1
						BTFSS				INTCON,T0IF						;REVISAR DESBORDAMIENTO
						GOTO				UNO
						GOTO				CERO


CERO					RLF					BYTE,1							;CORRER A LA IZQUIERDA ATRAVES DEL CARRY
						BCF					BYTE,0							;PONER BIT EN 0
						DECFSZ				CONTBIT,1						;DECREMENTAR CONTADOR DE BITS
						GOTO				CINCUENTA						;LA FORMACION DEL REGISTRO NO HA TERMINADO
						RETURN												;TERMINAR SUBRUTINA

UNO						RLF					BYTE,1							;CORRER A LA IZQUIERDA ATRAVES DEL CARRY
						BSF					BYTE,0							;PONER BIT EN 1
						DECFSZ				CONTBIT,1						;DECREMENTAR CONTADOR DE BITS
						GOTO				CINCUENTA						;LA FORMACION DEL REGISTRO NO HA TERMINADO
						RETURN												;TERMINAR SUBRUTINA

;;;;;;;;;;;;;;;;;;;;;;;;SUBRUTINA PARA TRANSMISIÓN DE DATOS

ENVIO					MOVWF				TXREG							;ENVIAR CONVERSIÓN POR EL PUERTO SERIAL
TXN						BTFSS				PIR1,TXIF						;BANDERA DE TRANSMISION ?
						GOTO				TXN
						BTFSS				PIR1,TXIF						;BANDERA DE TRANSMISION ?
						GOTO				TXN
						BCF					PIR1,TXIF						;BAJAR LA BANDERA DE TRANSMISION
						RETURN



						END

