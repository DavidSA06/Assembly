		LIST P=16F877A
		INCLUDE "P16F877A.INC"
		__CONFIG _CP_OFF & _WDT_OFF & _PWRTE_ON & _XT_OSC & _LVP_OFF & _BODEN_OFF

;;;;;;;;;;;;;;;;;;;;;;;;INICIALIZACION DE VARIABLES

ANS1					EQU					20H								;BYTE DE RECEPCION
CONTC					EQU					21H								;CONTADOR DE CICLOS	20:1
VALORL					EQU					22H								;BYTES USADOS POR EL CONVERSOR ANALÓGICO DIGITAL
VALORH					EQU					23H
CONTBIT					EQU					24H								;CONTADOR DE CORRIMIENTO
BYTE					EQU					25H								;BYTE DE ALMACENAMIENTO
REGISTRO1				EQU					26H								;BYTES DE LOS DATOS RECABADOS AL SENSOR DHT22
REGISTRO2				EQU					27H
REGISTRO3				EQU					28H
REGISTRO4				EQU					29H
REGISTRO5				EQU					30H
SENSOR1L				EQU					31H								;BYTES DE LOS DATOS CONVERTIDOS A SENSORES ANALOGICOS
SENSOR1H				EQU					32H
SENSOR2L				EQU					33H
SENSOR2H				EQU					34H
HUMEDADA				EQU					35H								;DATOS GUARDADOS PARA ENVIO
HUMEDADB				EQU					36H
TEMPERATURAA			EQU					37H
TEMPERATURAB			EQU					38H
CONTT					EQU					39H								;CONTEO DE TIEMPO PARA RETRASO



RESET

						ORG					00H
						GOTO				INICIO							;NOS SALTAMOS EL REGISTRO DE INTERRUPCION

						ORG					08H

;;;;;;;;;;;;;;;;;;;;;;;;CONFIGURACION DE ENTRADAS DEL PIC

INICIO					BSF					STATUS,RP0						;CAMBIO AL BANCO 1
						BCF					STATUS,RP1						;CAMBIO AL BANCO 1
						MOVLW				B'00011111'
						MOVWF				TRISA							;ENTRADAS PARA SENSORES ANALOGICOS
						MOVLW				B'00000000'
						MOVWF				TRISB							;PINES DE B COMO COMO SALIDAS PARA LED
						MOVLW				B'10000000'						;PIN RA6 SALIDA DEL PUERTO SERIAL (TX)
						MOVWF				TRISC							;PIN RA7 ENTRADA DEL PUERTO SERIAL (RX)
						MOVLW				B'00000111'						;ENTRADAS PARA CONFIGURACION DE SENSORES DIGITALES
						MOVWF				TRISE							;Y ENTRADA PARA ANALOGICO

;;;;;;;;;;;;;;;;;;;;;;;;CONFIGURACION DE PULL-UP Y TEMPORIZACION

						MOVLW				B'00000001'						;PULL-UP HABILITADO
						MOVWF				OPTION_REG						;PREESCALADOR 1:4

;;;;;;;;;;;;;;;;;;;;;;;;CONFIGURACIÓN DEL PUERTO SERIAL

						MOVLW				D'25'							;VELOCIDAD 9600  BAUDIOS
						MOVWF				SPBRG							;BAUDIOS Fosc/(K*(X+1)) X=SPBRG
						BCF					TXSTA,TX9						;TRANSMISION A 8 BITS EN VEZ DE 9
						BCF					TXSTA,SYNC						;USART MODO ASINCRONO
						BSF					TXSTA,BRGH						;BAUDIOS RATE HIGH SPEED K=16 (MENOR ERROR A 9600)
						BSF					TXSTA,TXEN						;TRANSMISION HABILITADA
						BCF					STATUS,RP0						;CAMBIO A BANCO 0
						BSF					RCSTA,SPEN						;PUERTO SERIAL HABILITADO
						BCF					RCSTA,RX9						;RECEPCION A 8 BITS
						BSF					RCSTA,CREN						;RECIBIMIENTO CONTINUO HABILITADO

;;;;;;;;;;;;;;;;;;;;;;;;CONFIGURACION DE INTERRUPCIONES

						CLRF				INTCON							;INTERRUPCIONES DESHABILITADAS

;;;;;;;;;;;;;;;;;;;;;;;;CONFIGURACIÓN DEL CONVERSOR ANALOGICO-DIGITAL

						MOVLW 				B'01000001'						;TOSC*32,CANAL 0,CONVERSION NO ACTIVA,CONVERSOR ENCENDIDO
						MOVWF				ADCON0
						BSF					STATUS, RP0						;CAMBIO A BANCO 1
						MOVLW				B'10001001'						;ADRESL, TOSC*32, JUSTIFICADO A LA DERECHA 6 BITS VACIOS EN ADRESH
						MOVWF				ADCON1							;AN7 /D/D/A/A/A/A/A/A/ AN0
						BCF					STATUS,RP0						;CAMBIO A BANCO 0

;;;;;;;;;;;;;;;;;;;;;;;;LIMPIEZA DE REGISTROS

						CLRF				PORTB							;REINICIO DE BYTES IMPORTANTES
						MOVLW				.10
						MOVWF				CONTC							;CONTADOR DE CICLOS	10:1 (2 SEGUNDOS)
						CLRF				VALORL
						CLRF				VALORH
						CLRF				CONTBIT
						CLRF				BYTE
						CLRF				REGISTRO1
						CLRF				REGISTRO2
						CLRF				REGISTRO3
						CLRF				REGISTRO4
						CLRF				REGISTRO5

;;;;;;;;;;;;;;;;;;;;;;;;RECEPCION DE DATOS

RCN						BTFSS				PIR1,RCIF						;ESPERAR INICIO DEL PROGRAMA
						GOTO				RCN
						BCF					PIR1,RCIF						;BAJAR LA BANDERA DE RECEPCION
						MOVFW				RCREG							;REGISTRO DE RECEPCION DE USART
						MOVWF				ANS1							;MOVER A LA CONSTANTE ANS1
						DECFSZ				CONTC,1
						GOTO				CAD

;;;;;;;;;;;;;;;;;;;;;;;;CAMBIAR A RUNNING STATUS (TRANSMISIÓN DEL PIC)

						MOVLW				.10
						MOVWF				CONTC
						BCF					INTCON,T0IF						;BAJAR LA BANDERA T0IF
						BSF					STATUS,RP0						;CAMBIO AL BANCO 1
						MOVLW				B'00000001'						;CAMBIAR PINES DIGITALES COMO SALIDA PARA DHT22
						MOVWF				TRISE							;Y ENTRADA PARA ANALOGICO
						BCF					STATUS,RP0						;CAMBIO AL BANCO 0
						BCF					PORTE,1							;ENVIAR SENAL GND AL SENSOR
						CALL				RETRASO							;20 ms
						BCF					STATUS,RP1
						BSF					STATUS,RP0						;CAMBIO AL BANCO 1
						MOVLW				B'00000111'						;CAMBIAR PINES DIGITALES COMO ENTRADA PARA DHT22
						MOVWF				TRISE							;Y ENTRADA PARA ANALOGICO
						BCF					STATUS,RP1
						BCF					STATUS,RP0						;CAMBIO AL BANCO 0
						BTFSC				PORTE,1							;ESPERAR A LA RESPUESTA BAJA DEL SENSOR POR 20~40 us
						GOTO				$-1
						BTFSS				PORTE,1							;ESPERAR A LA RESPUESTA ALTA DEL SENSOR POR 80 us
						GOTO				$-1
						BTFSC				PORTE,1							;ESPERAR EL INICIO DE LA TRANSMISION POR 80 us
						GOTO				$-1

;;;;;;;;;;;;;;;;;;;;;;;;LECTURA DE REGISTROS

;;;;;;;;;;;;;;;;;;;;;;;;PRIMER REGISTRO (HUMEDAD ALTO)
						CALL				CAPTURA
						MOVFW				BYTE
						MOVWF				REGISTRO1
;;;;;;;;;;;;;;;;;;;;;;;;SEGUNDO REGISTRO (HUMEDAD BAJO)
						CALL				CAPTURA
						MOVFW				BYTE
						MOVWF				REGISTRO2
;;;;;;;;;;;;;;;;;;;;;;;;TERCER REGISTRO (TEMPERATURA ALTO)
						CALL				CAPTURA
						MOVFW				BYTE
						MOVWF				REGISTRO3
;;;;;;;;;;;;;;;;;;;;;;;;CUARTO REGISTRO (TEMPERATURA BAJO)
						CALL				CAPTURA
						MOVFW				BYTE
						MOVWF				REGISTRO4
;;;;;;;;;;;;;;;;;;;;;;;;QUINTO REGISTRO (PARIDAD)
						CALL				CAPTURA
						MOVFW				BYTE
						MOVWF				REGISTRO5
						BTFSS				PORTE,1							;ESPERAR 50 us
						GOTO				$-1
;;;;;;;;;;;;;;;;;;;;;;;;TERMINA REGISTRO DE DATOS

;;;;;;;;;;;;;;;;;;;;;;;;COMPROBACION DE DATOS
						CALL				SUMA

;;;;;;;;;;;;;;;;;;;;;;;;CONVERSION DE DATOS

;;;;;;;;;;;;;;;;;;;;;;;;PRIMER CANAL

CAD						BCF					ADCON0,3						;SELECCION DE CANAL 0
						BCF					ADCON0,4
						BCF					ADCON0,5
						CALL				CONVERSION
						MOVFW				VALORL
						MOVWF				SENSOR1L
						MOVFW				VALORH
						MOVWF				SENSOR1H

;;;;;;;;;;;;;;;;;;;;;;;;PRIMER CANAL

						BSF					ADCON0,3						;SELECCION DE CANAL 1
						BCF					ADCON0,4
						BCF					ADCON0,5
						CALL				CONVERSION
						MOVFW				VALORL
						MOVWF				SENSOR2L
						MOVFW				VALORH
						MOVWF				SENSOR2H

;;;;;;;;;;;;;;;;;;;;;;;;TRANSMISION DE DATOS DIGITALES

;;;;;;;;;;;;;;;;;;;;;;;;PRIMER REGISTRO (HUMEDAD ALTO)
						MOVFW				REGISTRO1;HUMEDADA
						CALL				ENVIO
;;;;;;;;;;;;;;;;;;;;;;;;SEGUNDO REGISTRO (HUMEDAD BAJAO)
						MOVFW				REGISTRO2;HUMEDADB
						CALL				ENVIO
;;;;;;;;;;;;;;;;;;;;;;;;TERCER REGISTRO (TEMPERATURA ALTO)
						MOVFW				REGISTRO3;TEMPERATURAA
						CALL				ENVIO
;;;;;;;;;;;;;;;;;;;;;;;;CUARTO REGISTRO (TEMPERATURA BAJO)
						MOVFW				REGISTRO4;TEMPERATURAB
						CALL				ENVIO
;;;;;;;;;;;;;;;;;;;;;;;;QUINTO REGISTRO (PARIDAD)
						MOVFW				REGISTRO5
						CALL				ENVIO

;;;;;;;;;;;;;;;;;;;;;;;;TRANSMISION DE DATOS DEL CONVERSOR

;;;;;;;;;;;;;;;;;;;;;;;;TRANSMISION DE BYTE MENOS SIGNIFICATIVO DEL PRIMER SENSOR
						MOVFW				SENSOR1L
						CALL				ENVIO
;;;;;;;;;;;;;;;;;;;;;;;;TRANSMISION DE BYTE MAS SIGNIFICATIVO DEL PRIMER SENSOR
						MOVFW				SENSOR1H
						CALL				ENVIO

;;;;;;;;;;;;;;;;;;;;;;;;TRANSMISION DE BYTE MENOS SIGNIFICATIVO DEL SEGUNDO SENSOR
						MOVFW				SENSOR2L
						CALL				ENVIO
;;;;;;;;;;;;;;;;;;;;;;;;TRANSMISION DE BYTE MAS SIGNIFICATIVO DEL SEGUNDO SENSOR
						MOVFW				SENSOR2H
						CALL				ENVIO

;;;;;;;;;;;;;;;;;;;;;;;;TERMINA TRANSMISION DE DATOS, SE REINICIA EL PROGRAMA

						GOTO				RCN								;VOLVEMOS A ESPERAR LA ORDEN DE INICIO

;;;;;;;;;;;;;;;;;;;;;;;;SUBRUTINAS

;;;;;;;;;;;;;;;;;;;;;;;;SUBRUTINA PARA DESPERTAR EL MODULO AM2302

RETRASO					MOVLW				.20
						MOVWF				CONTC							;CARGAR CONTADOR
						MOVLW				.7								;992us (1000 MINIMO,20000 MAXIMO)
						MOVWF				TMR0							;COMIENZA EL CONTEO
						BTFSS				INTCON,T0IF						;REVISAR EL DESBORDAMIENTO
						GOTO				$-1
						BCF					INTCON,T0IF						;BAJAR LA BANDERA T0IF
						DECFSZ				CONTC,1							;REVISAR CONTADOR
						GOTO				$-6
						RETURN

;;;;;;;;;;;;;;;;;;;;;;;;SUBRUTINA DE CAPTURA DE DATOS POR BYTE

CAPTURA					CLRF				BYTE
						MOVLW				.8
						MOVWF				CONTBIT							;CARGAR CONTADOR DE BITS
CINCUENTA				BTFSS				PORTE,1							;ESPERAR 50 us
						GOTO				$-1
						BCF					INTCON,T0IF						;BAJAR LA BANDERA T0IF 
						MOVLW				.246
						MOVWF				TMR0							;CONTAR 40us (26~28us A 70us)
						BTFSC				PORTE,1							;ESPERAR EL FINAL DEL BIT
						GOTO				$-1
						BTFSS				INTCON,T0IF						;REVISAR DESBORDAMIENTO
						GOTO				CERO
						GOTO				UNO


CERO					RLF					BYTE,1							;CORRER A LA IZQUIERDA ATRAVES DEL CARRY
						BCF					BYTE,0							;PONER BIT EN 0
						DECFSZ				CONTBIT,1						;DECREMENTAR CONTADOR DE BITS
						GOTO				CINCUENTA						;LA FORMACION DEL REGISTRO NO HA TERMINADO
						RETURN												;TERMINAR SUBRUTINA

UNO						RLF					BYTE,1							;CORRER A LA IZQUIERDA ATRAVES DEL CARRY
						BSF					BYTE,0							;PONER BIT EN 1
						DECFSZ				CONTBIT,1						;DECREMENTAR CONTADOR DE BITS
						GOTO				CINCUENTA						;LA FORMACION DEL REGISTRO NO HA TERMINADO
						RETURN												;TERMINAR SUBRUTINA

;;;;;;;;;;;;;;;;;;;;;;;;SUBRUTINA PARA SUMAR

SUMA					MOVFW				REGISTRO1						;MOVER SUMA1 A W
						ADDWF				REGISTRO2,0						;SUMAR SUMA1 Y SUMA2
						ADDWF				REGISTRO3,0
						ADDWF				REGISTRO4,0
						BCF					STATUS,Z						;BAJAR LA BANDERA
						SUBWF				REGISTRO5,0						;RESTARLE LA SUMA A LA PARIDAD
						BTFSS				STATUS,Z						;REVISAR SI SON IGUALES
						RETURN												;NO SON IGUALES ENVIAR REGISTROS ANTERIORES
						BCF					STATUS,Z						;BAJAR LA BANDERA
						MOVFW				REGISTRO1						;MODIFICAR REGISTROS PUES LA MEDICION FUE CORRECTA
						MOVWF				HUMEDADA
						MOVFW				REGISTRO2
						MOVWF				HUMEDADB
						MOVFW				REGISTRO3
						MOVWF				TEMPERATURAA
						MOVFW				REGISTRO4
						MOVWF				TEMPERATURAB
						RETURN

;;;;;;;;;;;;;;;;;;;;;;;;SUBRUTINA DE CONVERSION DE DATOS

CONVERSION				BSF					ADCON0,ADON						;ACTIVAR EL MODULO DE CONVERSION
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						NOP
						BSF					ADCON0,GO_DONE					;CONVERSION EN CURSO
ESPERA					BTFSC				ADCON0,GO_DONE					;ESPERAR AL FINAL DE LA CONVERSION
						GOTO				ESPERA
						BCF					ADCON0,ADON						;DESACTIVAR EL MODULO DE CONVERSION	
						BCF					PIR1,ADIF						;BAJAR LA BANDERA DE CONVERSION

;;;;;;;;;;;;;;;;;;;;;;;;EXTRACCION DE DATOS 
						BSF					STATUS,RP0						;CAMBIO AL BANCO 1
						MOVFW				ADRESL							;OBTENER LOS 8 BITS MENOS SIGNIFICATIVOS
						BCF					STATUS,RP0						;CAMBIO AL BANCO 0
						MOVWF				VALORL							;GUARDAR DATOS MENOS SIGNIFICATIVOS
						MOVWF				PORTB							;MOSTRAR CONVERSION EN LOS LEDS
						MOVFW				ADRESH							;OBTENER LOS 2 BITS MAS SIGNIFICATIVOS
						MOVWF				VALORH							;GUARDAR DATOS MAS SIGNIFICATIVOS
						RETURN


;;;;;;;;;;;;;;;;;;;;;;;;SUBRUTINA PARA TRANSMISIÓN DE DATOS

ENVIO					MOVWF				TXREG							;ENVIAR CONVERSIÓN POR EL PUERTO SERIAL
TXN						BTFSS				PIR1,TXIF						;BANDERA DE TRANSMISION ?
						GOTO				TXN
						BTFSS				PIR1,TXIF						;BANDERA DE TRANSMISION ?
						GOTO				TXN
						BCF					PIR1,TXIF						;BAJAR LA BANDERA DE TRANSMISION
						RETURN



						END